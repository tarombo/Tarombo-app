apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def fileChiavi = rootProject.file("chiavi.properties")
def chiave

android {
	if( fileChiavi.exists() ) {
		chiave = new Properties()
		chiave.load(new FileInputStream(fileChiavi))
		signingConfigs {
			configa {
				keyAlias chiave['keyAlias']
				keyPassword chiave['keyPassword']
				storeFile file(chiave['storeFile'])
				storePassword chiave['storePassword']
			}
		}
	}

	compileSdkVersion 34
	defaultConfig {
		applicationId "org.siboro.tarombo"
		minSdkVersion 24
		targetSdkVersion 34
		versionCode 50
		versionName '0.5.4 (3bd9411)'
		vectorDrawables.useSupportLibrary = true
		resConfigs "en", "cs", "de", "eo", "es", "fa", "fr", "hr", "hu", "in", "it", "iw", "kn", "mr", "nb", "nl", "pl", "pt", "ru", "sk", "sr", "uk"
		def utenteAruba = ""
		def passwordAruba = ""
		def utenteGeoNames = "demo"
		if (chiave) {
			utenteAruba = chiave['utenteAruba']
			passwordAruba = chiave['passwordAruba']
			utenteGeoNames = chiave['utenteGeoNames']
		}
		buildConfigField "String", "utenteAruba", "\""+utenteAruba+"\""
		buildConfigField "String", "passwordAruba", "\""+passwordAruba+"\""
		buildConfigField "String", "utenteGeoNames", "\""+utenteGeoNames+"\""
		buildConfigField "boolean", "allowAds", "true"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		multiDexEnabled true
		signingConfig signingConfigs.debug
	}
	buildTypes {
		debug {
			buildConfigField "String", "AD_UNIT_ID", "\"ca-app-pub-3940256099942544/5354046379\"" //this is test ad unit id for Rewarded Video Ad (during we have to use testing ad id)
			buildConfigField "String", "AD_BANNER_UNIT_ID", "\"ca-app-pub-3940256099942544/9214589741\""
			pseudoLocalesEnabled true
			if (chiave)
				signingConfig signingConfigs.configa
		}
		'debug (No Ads)' {
			initWith debug
			buildConfigField "boolean", "allowAds", "false"
		}
		release {
			buildConfigField "String", "AD_UNIT_ID", "\"ca-app-pub-5341101936519197/7846354630\"" //this is live ad unit id for Rewarded Video Ad (dont use it for testing)
			buildConfigField "String", "AD_BANNER_UNIT_ID", "\"ca-app-pub-5341101936519197/7801627762\""
			shrinkResources false
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			if (chiave)
				signingConfig signingConfigs.configa
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	packagingOptions {
		excludes += ['**/*.properties', 'META-INF/*.md', 'META-INF/*.xml']
	}
	buildFeatures {
		viewBinding true
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'org.familysearch.gedcom:gedcom:1.11.0'

	implementation platform('com.google.firebase:firebase-bom:30.1.0')
	implementation 'com.google.firebase:firebase-crashlytics'
	implementation 'com.google.firebase:firebase-analytics'

	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.lifecycle:lifecycle-process:2.6.2'
	implementation 'androidx.work:work-runtime:2.8.1'
	implementation 'com.google.android.material:material:1.10.0'
	implementation 'com.google.code.gson:gson:2.8.8'

	implementation 'commons-io:commons-io:2.5' // 2.7 will crash on asus android 7
	implementation 'commons-net:commons-net:3.6'
	implementation 'org.jsoup:jsoup:1.14.1'
	implementation 'joda-time:joda-time:2.10.10'
	implementation 'com.otaliastudios:zoomlayout:1.3.0'
	implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
	implementation 'com.squareup.picasso:picasso:2.71828'
	implementation 'org.jdom:jdom:1.1.3' // Don't update to version 2.x for compatibility with GeoNames
	implementation 'com.android.installreferrer:installreferrer:2.2'

	implementation project(path: ':oauthLibGithub')
	implementation 'androidx.appcompat:appcompat:1.6.1'

	implementation 'com.github.AndroidDeveloperLB:FastScrollerAndRecyclerViewFixes:5'
	implementation 'com.google.guava:guava:31.1-jre'

	//implementation 'jaxen:jaxen:1.2.0'
	androidTestImplementation 'androidx.test:runner:1.5.2'

	implementation 'com.squareup.picasso:picasso:2.71828'

	implementation 'com.google.android.gms:play-services-ads:23.2.0'

	implementation "com.squareup.retrofit2:retrofit:2.9.0"
	implementation "com.squareup.retrofit2:converter-gson:2.9.0"
	implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
	implementation "com.squareup.okhttp3:okhttp:4.9.1"
	implementation "com.squareup.okhttp3:okhttp-urlconnection:4.9.1"
	implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
	implementation "com.squareup.moshi:moshi:1.12.0"

	implementation 'com.github.bumptech.glide:glide:4.13.2'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'
}

